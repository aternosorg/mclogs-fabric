plugins {
	id 'java'
	id 'fabric-loom' version '1.7.1'
	id "com.modrinth.minotaur" version "2.8.7"
	id "net.darkhax.curseforgegradle" version "1.1.15"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.hasProperty('release') ? project.getProperty('release') : 'dev'
group = project.maven_group
changelog = rootProject.file("CHANGELOG.md").text

dependencies {
	// Mclogs Java - Library to read, list and share logs to mclogs
	implementation 'gs.mclo:api:4.0.1'
	include 'gs.mclo:api:4.0.1'

	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
	def tag_version = project.version.split('-')[0]
	inputs.property "version", tag_version

	filesMatching("fabric.mod.json") {
		expand "version": tag_version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 16
}

jar {
	from "LICENSE"
}

loom {
	// serverOnlyMinecraftJar()
	// This produces a compilation error at the moment
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "6DdCzpTL"
	uploadFile = "./build/libs/mclogs-fabric-${project.version}.jar"
	loaders = ["fabric", "quilt"]
	gameVersions = [
			"1.19.3",
			"1.19.2",
			"1.19.1",
			"1.19",
	]
	changelog = rootProject.changelog
	dependencies {
		required.project "fabric-api"
		required.project "qsl"
	}
}

task publishCurseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("CURSEFORGE_TOKEN") ?: ''
	def mainFile = upload(420561, "./build/libs/mclogs-fabric-${project.version}.jar")
	mainFile.releaseType = 'release'
	mainFile.changelogType = 'markdown'
	mainFile.changelog = rootProject.changelog
	mainFile.addModLoader("Quilt")
	mainFile.addRequirement("fabric-api")
	mainFile.addRequirement("qsl")
	mainFile.addJavaVersion("Java ${JavaVersion.current().majorVersion}")
	mainFile.addGameVersion("1.19.3")
	mainFile.addGameVersion("1.19.2")
	mainFile.addGameVersion("1.19.1")
	mainFile.addGameVersion("1.19")
}
